plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '0.7.3'
}

group = "org.kohsuke.stapler.idea"
version = "2.0.1"

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        url "https://repo.jenkins-ci.org/releases/"
    }
}

dependencies {
    implementation('org.jenkins-ci:commons-jexl:1.1-jenkins-20111212') {
        // Provided by the Platform
        exclude group: "commons-logging", module: "commons-logging"
    }
    implementation 'net.java.dev.textile-j:textile-j:2.2.864'

    testImplementation 'junit:junit:4.13.2'
}

// See https://github.com/JetBrains/gradle-intellij-plugin/

// https://plugins.jetbrains.com/docs/intellij/build-number-ranges.html
// https://plugins.jetbrains.com/docs/marketplace/product-versions-in-use-statistics.html
// https://www.jetbrains.com/idea/download/other.html
intellij {
    version = ideaVersion
    plugins = ["properties"]
}

patchPluginXml {
    sinceBuild = "110.00"
    untilBuild = ""
    changeNotes = """
      <p>2.0.1:</p>
      <ul>
        <li>JEXL Inspection crashes are fixed by addressing logger initialization issue in JEXL</li>
        <li>Restore the association between *.jelly extension and XML language</li>
      </ul>
    """
}

runPluginVerifier {
    // Rather arbitrarily chosen first releases of last three year.
    // Product version in use statistics say last 3 major releases. It does not define "major release" though but
    // but educated guess is that `year.version` is what is considered major.
    ideVersions = ["2019.1.4", "2020.1.3", "2021.1"]
}

publishPlugin {
    token = intellijPublishToken
}