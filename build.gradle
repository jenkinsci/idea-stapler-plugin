plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '1.9.0'
}

group = "org.kohsuke.stapler.idea"
version = "2.0.7"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

repositories {
    mavenCentral()
    maven {
        url "https://repo.jenkins-ci.org/releases/"
    }
}

dependencies {
    implementation('org.jenkins-ci:commons-jexl:1.1-jenkins-20111212') {
        // Provided by the Platform
        exclude group: "commons-logging", module: "commons-logging"
    }
    implementation 'net.java.dev.textile-j:textile-j:2.2.864'

    testImplementation 'junit:junit:4.13.2'
}

tasks.withType(JavaCompile) {
    //enable compiler warnings
    options.deprecation = true
    options.compilerArgs << "-Xlint:all"
    // write them to a file so that Warnings NG plugin can parse them.
    doFirst {
        logging.addStandardErrorListener({ message ->
            file("${buildDir}/javac.log") << message
        } as StandardOutputListener)
    }
}

// See https://github.com/JetBrains/gradle-intellij-plugin/

// https://plugins.jetbrains.com/docs/intellij/build-number-ranges.html
// https://plugins.jetbrains.com/docs/marketplace/product-versions-in-use-statistics.html
// https://www.jetbrains.com/idea/download/other.html
intellij {
    version = ideaVersion
    plugins = platformPlugins.tokenize(',')*.trim()
}

patchPluginXml {
    sinceBuild = "203.00"
    untilBuild = ""
    changeNotes = """
      <h3>2.0.7</h3>
      <ul>
        <li>Fix the <code>StackOverflowError</code> in Stapler Custom Jelly Taglibrary</li>
        <li><code>*.jellytag</code> extension is recognized as Jelly. Stapler supports it for Jellyy Taglibraries</li>
        <li><code>stapler.xsd</code> is formatted to improve readability</li>
      </ul>
      <h3>2.0.6</h3>
      <ul>
        <li>Switch from deprecated imperative File Type definition to the declarative one. Internal change. No functional changes are expected.</li> 
      </ul>
      <h3>2.0.5</h3>
      <ul>
        <li>Compatibility baseline is changed to 2020.3 which also changes Java level to 11. No functional changes are expected.</li>
      </ul>
      <h3>2.0.4</h3>
      <ul>
        <li>Avoid <code>java.lang.NullPointerException</code> at <code>org.kohsuke.stapler.idea.JellyDocumentationProvider.generateDoc()</code>
        by short-circuiting on <code>null</code> in the nullable parameter.
        </li>
      </ul>
      <h3>2.0.3</h3>
      <ul>
        <li>Revert Jelly file type introduced in 2.0.1. It turned out that there is regression in Intellij 2021.2 which 
        prevents adding extensions to existing File Types (IDEA-277310). Attempt to work it around caused more issues than
         it fixed. Regression should be fixed in 2021.3. Meanwhile *.jelly can be manually added to XML file type.</li>
      </ul>
      <h3>2.0.2</h3>
      <ul>
        <li>Deprecation scheduled for removal are addressed to maintain compatibility with 2021.3 onward</li>
      </ul>
      <h3>2.0.1</h3>
      <ul>
        <li>JEXL Inspection crashes are fixed by addressing logger initialization issue in JEXL</li>
        <li>Restore the association between *.jelly extension and XML language</li>
      </ul>
    """
}

runPluginVerifier {
    // Rather arbitrarily chosen first releases of last three year.
    // Product version in use statistics say last 3 major releases. It does not define "major release" though but
    // but educated guess is that `year.version` is what is considered major.
    ideVersions = ["2020.3.2", "2021.3.3", "2022.1"]
}

publishPlugin {
    token = intellijPublishToken
}